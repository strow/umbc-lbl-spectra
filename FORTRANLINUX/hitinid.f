C***********************************************************************
C
C  PROGRAM        INIT  SUBROUTINE
C
C  PURPOSE        INITIALISING ROUTINE FOR READING LINE DATA FILES
C
C  VERSION        3.0   D.P. EDWARDS, D. ROISIN   02/05/90 
C
C  DESCRIPTION    SUBROUTINE FOR INITIALIZING THE LINE DATA FILE.
C                 THIS ROUTINE CAN BE USED TO INITIALIZE THE 
C                 UNFORMATTED DIRECT ACCESS HITRAN FILE USING VAX 
C                 SPECIFIC FORTRAN WHEN USING A VAX OR USING STANDARD 
C                 FORTRAN77 FOR OTHER MACHINES. WHICH EVER IS USED, 
C                 THE CODE SECTIONS RELEVANT TO THE OTHER OPTION 
C                 SHOULD BE COMMENTED OUT BELOW.
C
C  ARGUMENTS      INFILE  C*80 I/P HITRAN LINEBASE FILENAME
C                 WNUMLO  I*4  I/P LOWEST WAVENUMBER OF INTEREST [cm-1]
C                 LUN     I*4  I/P STREAM FOR READING LINEBASE
C                 ISUM    I*4  I/P NUMBER OF GASES TO BE READ
C                 IDG     I*4  I/P ID'S OF REQUIRED GASES
C                 ICH     I*4  I/P LINE VERSIONS OF REQUIRED GASES
C                 TITLB   C*48 O/P TITLE OF LINEBASE  
C
C  I/O UNITS      LUN    I/P   INPUT LINE DATA FILE   
C                 IOD    O/P   FORMATTED SUMMARY OUTPUT FILE
C
C  SYSTEM SPECIFIC                 VAX OPEN STATEMENT: READONLY, RECL
C                                  CRAY OPEN STATEMENT: RECL
C                                  VAX BYTE DATA EQUIVALENCE STATEMENTS
C***********************************************************************
C
       SUBROUTINE INIT
     + (INFILE,WNUMLO,LUN,ISUM,IDG,ICH,TITLB)
C-----------------------------------------------------------------------
	integer mxgas, LUN,ISUM, IG,IOST,K,l 
	real*8 WNUMLO
       PARAMETER (MXGAS=15)
C
C  COMMON BLOCK CONTAINING I/O UNITS
C
       COMMON /COMUNT/ IOD

       INTEGER IOD
C
C  COMMON BLOCKS HITC1 AND HITC2 CONTAIN THE VARIABLES READ IN ONE 
C  RECORD OF THE LINE DATA BASE. HITC1 CONTAINS NUMERIC VARABLES AND 
C  HITC2 CHARACTER.
C
       COMMON /COMHT1/ IDUM,LSTAT,IGAS,ISO,WNUM,STREN,TPROB,ABROAD,
     +                SBROAD,ELS,ABCOEF,TSP,IUSGQ,ILSGQ
       COMMON /COMHT2/ USLQ,BSLQ,AI,REF
C
       INTEGER          IDUM,LSTAT,IGAS,ISO,IUSGQ,ILSGQ,IFWDPT
       real*4             STREN,TPROB,ABROAD,SBROAD,ELS,ABCOEF,TSP
       DOUBLE PRECISION WNUM
       CHARACTER*3      AI
       CHARACTER*6      REF
       CHARACTER*9      USLQ,BSLQ
C
C  COMMON BLOCK COMHIT IS USED TO COMMUNICATE BETWEEN HITINI AND HITRD;
C  IT IS NOT INTENDED TO BE READ OUTSIDE THESE ROUTINES.
C
       COMMON /COMHIT/ LUNX,NOGAS,IFWD,IGSNUM,IDGX,ICHX,IFL,
     +                 IREC1,IREC2,IREC
C
       INTEGER     LUNX,NOGAS,IFWD(MXGAS),
     +             IGSNUM,IDGX(MXGAS),ICHX(MXGAS),
     +             IFL,IREC1,IREC2,IREC
C
       INTEGER          IDG(MXGAS),ICH(MXGAS)
       DOUBLE PRECISION WNUMLX
       LOGICAL          INITIAL
       CHARACTER        TITLB*48,INFILE*80,FILMIX*80,WORDIN*70
       INITIAL = .TRUE.
C
C$$$$$$ VAX SPECIFIC SECTION $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C       
C       BYTE        IBUF1(56),IBUF2(32)                  
C       EQUIVALENCE (IBUF1(1),LSTAT),(IBUF2(1),USLQ)    
C       EQUIVALENCE (IBUF2(28),IFWDPT)                  
C      
C$$$$$$ END OF VAX SPECIFIC SECTION $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C
C       SAVE          LLS COMMENTED OUT
C-----------------------------------------------------------------------
C       
C  SET UP COMMON BLOCK VARAIABLES
C
       LUNX = LUN
       IGSNUM = ISUM
       DO 10 IG=1,IGSNUM
         IDGX(IG) = IDG(IG)
         ICHX(IG) = ICH(IG)
   10  CONTINUE
C
C  CHECK INITIALISATION STATUS AND OPEN LINEBASE FILE
C
       IF (INITIAL) THEN
C
C
C$$$$$$ STANDARD SECTION $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C  FOR USE WITH HITLIN MACH=1 OPTION
C  4 BYTE/WORD MACHINES, RECL IN WORDS
C
         OPEN(LUNX,FILE=INFILE,STATUS='OLD',
     +   IOSTAT=IOST,ACCESS='DIRECT',RECL=22)
C$$$$$$ END OF STANDARD SECTION $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C
C$$$$$$ VAX SPECIFIC SECTION $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C  FOR USE WITH HITLIN MACH=2 OPTION
C  4 BYTE/WORD VAX, RECL IN WORDS
C
C         OPEN(LUNX,FILE=INFILE,STATUS='OLD',
C     +   IOSTAT=IOST,ACCESS='DIRECT',RECL=22,READONLY)             
C$$$$$$ END OF VAX SPECIFIC SECTION $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C
C$$$$$$ CRAY SPECIFIC SECTION $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C  FOR USE WITH HITLIN MACH=3 OPTION
C  8 BYTE/WORD CRAY, RECL IN BYTES
C
C         OPEN(LUNX,FILE=INFILE,STATUS='OLD',
C     +   IOSTAT=IOST,ACCESS='DIRECT',RECL=152)
C$$$$$$ END OF CRAY SPECIFIC SECTION $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C
C  CHECK STATUS OF .BIN FILE AFTER OPENING
C
         IF (IOST.NE.0) THEN
           WRITE(IOD,1000)IOST
 1000      FORMAT(
     +     1X,'!!!!!!!!!! SUBROUTINE HITINI !!!!!!!!!!',/,
     +     1X,'ERROR: IOST =',I5                       ,/,
     +     1X,'TRYING TO OPEN LINE DATA FILE          ')
           call mexerrmsgtxt('HITINI')
         ENDIF
C
C  GET FROM FILE HEADER RECORD 
C
         READ(LUNX,REC=1) LSTAT,NOGAS,
     +   IREC1,IREC2,TITLB

       ENDIF
C
C  DO SEARCH IN DOUBLE PRECISION SINCE WNUM IS IN DP
C       
       WNUMLX = WNUMLO
C
C  BINARY SEARCH FOR 1ST RECORD: FOLLOWS CODE IN THE IBM SLUP ROUTINE:
C  K IS FIRST RECORD, L IS LAST
C
       K = IREC1
       L = IREC2
   30  IREC = (K + L)/2
C
C  ONLY NEED WNUM FOR SEARCH
C
C$$$$$$ VAX SPECIFIC SECTION $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C       READ(LUNX,REC=IREC) (IBUF1(I),I=1,20) 
C$$$$$$ END OF VAX SPECIFIC SECTION $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C
C$$$$$$ STANDARD SECTION $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      READ(LUNX,REC=IREC) LSTAT,IGAS,ISO,WNUM
C$$$$$$ END OF STANDARD SECTION $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C
C
       IF (WNUM .LT. WNUMLX) THEN
C
C  REQUIRED WN HIGHER, SO TRY HALF WAY UP TO CURRENT L
C
         K = IREC
       ELSE
C
C  REQUIRED WN LOWER OR EQUAL, SO TRY HALF WAY DOWN TO CURRENT K
C
         L = IREC
       ENDIF
C
C  IF K & L DIFFER BY 1, HAVE FOUND REQUIRED LOCATION WHERE THE K 
C  WAVENUMBER IS LOWER THAN WNUMLO, AND THE L WAVENUMBER IS GREATER 
C  THAN OR EQUAL TO WNUMLO. CHOOSE RECORD L (NOTE K<L). IF THERE IS 
C  MORE THAN 1 RECORD AT EXACTLY WAVENUMBER WNUMLO, WILL FINISH 
C  POINTING TO FIRST.  FORWARD POINTER BLOCK ARE LABELLED WITH 
C  WAVENUMBER OF NEXT LINE TO EXPLOIT THIS.
C
       IF (K+1 .NE. L) GO TO 30
C
C  NOTE HITRD INCREMENTS IREC BEFORE READING SO WILL START ON L
C
       IREC = K
       IF (IREC .EQ. IREC1) IREC=IREC-1
C
C  SET UP ARRAY OF FORWARD POINTERS TO BE EMPTY:
C
       DO 40 IG=1,IGSNUM
         IFWD(IG) = 0
   40  CONTINUE
C
       IFL = IGSNUM
       RETURN
       END


